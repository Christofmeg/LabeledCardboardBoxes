import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'idea'
    id 'net.darkhax.curseforgegradle' version '1.+'
    id 'com.modrinth.minotaur' version '2.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

archivesBaseName = "${mod_id}-forge-${supported_versions}"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: 'official', version: minecraft_version

    runs {
        client {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Client'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                modClientRun {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Data'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            args '--mod', mod_id, '--all',
                    '--output', file('src/generated/resources/'),
                    '--existing', file('src/main/resources/'),
                    '--existing', file('src/test/resources/')
            mods {
                modDataRun {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Server'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                modServerRun {
                    source sourceSets.main
                }
            }
        }

    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources', 'src/generated/resources']
        }
    }
}

repositories {
	maven {
		// location of a maven mirror for JEI files, as a fallback
		name = "ModMaven"
		url = "https://modmaven.dev/"
		content {
			includeGroup "mezz.jei"
		}
	}
	maven {
		url "https://www.cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    // JEI
    if (project.hasProperty('jei_version')) {
        runtimeOnly(fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}"))
    }
    // CATALOGUE
    if (project.hasProperty('forge_catalogue_version')) {
        implementation fg.deobf("curse.maven:catalogue-459701:${forge_catalogue_version}")
    }
    // CONFIGURED
    if (project.hasProperty('forge_configured_version')) {
        implementation fg.deobf("curse.maven:configured-457570:${forge_configured_version}")
    }

    implementation fg.deobf("curse.maven:mekanism-268560:3659389")
    implementation fg.deobf("curse.maven:the-one-probe-245211:3752096")

    // Only 1 at a time
//    implementation fg.deobf("curse.maven:jade-324717:3910873")
//    implementation fg.deobf("curse.maven:amber-jade-851652:4873092")
//    implementation fg.deobf("curse.maven:wthit-forge-455982:3831595")
    implementation fg.deobf("curse.maven:hwyla-253449:3033593")

}

processResources {
    exclude '.cache'
}

jar.finalizedBy('reobfJar')

tasks.register('publishCurseForge', TaskPublishCurseForge) {
    apiToken = file('../API Tokens/mekanismcardboardtooltip.md')
    def projectId = "${cf_id}".toString()
    def mainFile = upload(projectId, file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar"))
    mainFile.changelogType = 'markdown'
    mainFile.changelog = file('changelog.md')
    mainFile.addGameVersion('Client', 'Server')
    mainFile.releaseType = "${release_type}"
    "${forge_release_versions}".split(',').each {
        mainFile.addGameVersion("${it}")
    }
    mainFile.addRequirement("mekanism")
    mainFile.addOptional("amber-jade")
    mainFile.addOptional("jade")
    mainFile.addOptional("the-one-probe")
    mainFile.addOptional("wthit-forge")
    mainFile.addOptional("hwyla")
    mainFile.addOptional("catalogue")
    mainFile.addModLoader('Forge')
}

modrinth {
    token = file('../API Tokens/modrinth.md').text
    projectId = "${modrinth_id}"
    versionName = getArchivesBaseName() + "-" + getVersion()
    versionType = "${release_type}"
    changelog = file('changelog.md').text
    uploadFile = file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")
    gameVersions = "${forge_release_versions}".split(",") as List<String>
    dependencies {
        required.project "mekanism"
        optional.project "amber"
        optional.project "jade"
        optional.project "the-one-probe"
        optional.project "wthit"
        optional.project "hwyla"
    }
    loaders = "forge".split(",") as List<String>
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
}